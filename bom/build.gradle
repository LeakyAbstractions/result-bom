plugins {
    id 'java-platform'
    id 'maven-publish'
    id 'signing'
}

dependencies {
    constraints {
        api libs.result
        api libs.result.assertj
        api libs.result.jackson
        api libs.result.lazy
    }
}

// Configure publishing repositories and publications
publishing {
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri('https://maven.pkg.github.com/LeakyAbstractions/result')
            credentials {
                username = System.env.GITHUB_USERNAME
                password = System.env.GITHUB_PASSWORD
            }
        }
    }
    publications {
        mavenRelease(MavenPublication) {
            pom {
                groupId         = rootProject.group
                artifactId      = project.artifactId
                name            = project.artifactName
                description     = project.artifactDescription
                version         = rootProject.version
                url             = rootProject.homepage
                licenses {
                    license {
                        name    = rootProject.licenseName
                        url     = rootProject.licenseUrl
                    }
                }
                developers {
                    developer {
                        id      = rootProject.developerId
                        name    = rootProject.developerName
                        email   = rootProject.developerEmail
                    }
                }
                organization {
                    name        = rootProject.organizationName
                    url         = rootProject.organizationUrl
                }
                issueManagement {
                    system      = rootProject.issuesSystem
                    url         = rootProject.issuesUrl
                }
                ciManagement {
                    system      = rootProject.ciSystem
                    url         = rootProject.ciUrl
                }
                scm {
                    url         = rootProject.repository
                    connection  = rootProject.checkout
                }
                properties = [
                    "result.version":         "0.14.1.2",
                    "result.assertj.version": "0.8.0.0",
                    "result.jackson.version": "0.6.1.1",
                    "result.lazy.version":    "0.5.0.2"
                ]
            }
            from(components.javaPlatform)
        }
    }
}

signing {
    def signingKey = System.env.SIGNING_KEY
    def signingPassword = System.env.SIGNING_PASSWORD
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenRelease
}

publish {
    finalizedBy publishToSonatype
}

publishToSonatype {
    finalizedBy ':closeAndReleaseSonatypeStagingRepository'
}
